#Использовать restler
#Использовать json

#Область ОписаниеПеременных

Перем КлючАвторизации;
Перем СекретныйКлюч;
Перем ТокенАвторизации;

Перем ПарсерJSON;
Перем ВерсияAPI;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ПриСозданииОбъекта(пКлючАвторизации = "", пСекретныйКлюч = "", пТокенАвторизации = "")
	
	УстановитьПараметрыАвторизации(пКлючАвторизации, пСекретныйКлюч);
	УстановитьТокенАвторизации(пТокенАвторизации);
	
КонецПроцедуры // ПриСозданииОбъекта()

Процедура УстановитьПараметрыАвторизации(пКлючАвторизации, пСекретныйКлюч) Экспорт
	КлючАвторизации = пКлючАвторизации;
	СекретныйКлюч = пСекретныйКлюч;
КонецПроцедуры // УстановитьПараметрыАвторизации()

Процедура УстановитьТокенАвторизации(пТокенАвторизации) Экспорт
	ТокенАвторизации = пТокенАвторизации;
КонецПроцедуры // УстановитьТокенАвторизации()

Функция ПолучитьТокенАвторизации() Экспорт
	
	// TODO: В разрабоке, нужно допилить получение токена

	Результат = Ложь;

	Соединение = Новый HTTPСоединение("https://api.dropboxapi.com", 443);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Basic " + ПолучитьХэшАвторизации());
	Заголовки.Вставить("Content-Type", "application/json");

	Клиент = Новый КлиентВебAPI();
	Клиент.ИспользоватьСоединение(Соединение);
	Клиент.УстановитьЗаголовки(Заголовки);
	Клиент.УстановитьТипКонтентаТелаЗапроса(ТипКонтентаТелаЗапроса.JSON);	
	Клиент.ДобавитьПараметр("oauth1_token", КлючАвторизации);
	Клиент.ДобавитьПараметр("oauth1_token_secret", СекретныйКлюч);
	
	ОтветСервиса = Клиент.Отправить(ВерсияAPI + "/auth/token/from_oauth1");

	// Получитьм токен

	Возврат Результат;
	
КонецФункции // ПолучитьТокенАвторизации()

Функция ОтправитьФайл(ВходящиеДанные, ПутьНаСервисе, ИспользоватьСессии = Ложь, Режим = "add", 
	Перемеименовывать = Истина, НеуведомлятьПользователей = Ложь, КонтролироватьСтруктуру = Ложь) Экспорт
	
	Перем ЛокальныйПутьКФайлу;
	Если ТипЗнч(ВходящиеДанные) = Тип("Строка") Тогда
		ЛокальныйПутьКФайлу = ВходящиеДанные;
	ИначеЕсли ТипЗнч(ВходящиеДанные) = Тип("ОбъектDropbox") Тогда
		ЛокальныйПутьКФайлу = ВходящиеДанные.Путь;
	Иначе
		ВызватьИсключение("Во входящие данные передан не совместимый тип. Совместимые типы: Строка Или ОбъектDropbox");	
	КонецЕсли;


	ЛокальныйФайл = Новый Файл(ЛокальныйПутьКФайлу);
	Если Не ЛокальныйФайл.Существует() Тогда
		ВызватьИсключение ("Локальный файл не существует");
	КонецЕсли;
	
	РазмерФайла  = ЛокальныйФайл.Размер();
	Если ИспользоватьСессии Или РазмерФайла <= ПолучитьОграничениеРазмераФайла() Тогда
		Возврат ОтправитьФайлБезСессии(ВходящиеДанные, ЛокальныйПутьКФайлу, ПутьНаСервисе, Режим, Перемеименовывать,
		НеуведомлятьПользователей, КонтролироватьСтруктуру);
	КонецЕсли;
	
	ИдентификаторФайла = Неопределено;
	
	ИдентификаторСессии = ОткрытьСессиюОтправкиФайла(ЛокальныйПутьКФайлу);
	Если ИдентификаторСессии <> Неопределено Тогда
		
		Если ОтправитьФайлЧерезСессию(ИдентификаторСессии, ЛокальныйПутьКФайлу, 0) Тогда
			
			ПараметрыДействия = Новый Структура();
			ПараметрыДействия.Вставить("path", ПутьНаСервисе);
			ПараметрыДействия.Вставить("mode", Режим);
			ПараметрыДействия.Вставить("autorename", Перемеименовывать);
			ПараметрыДействия.Вставить("mute", НеуведомлятьПользователей);
			ПараметрыДействия.Вставить("strict_conflict", КонтролироватьСтруктуру);
			
			ВходящиеДанные = ЗакрытьСесииюОтправкиФайла(ВходящиеДанные, ИдентификаторСессии, ЛокальныйПутьКФайлу, ПараметрыДействия, РазмерФайла);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИдентификаторФайла; 
	
КонецФункции // ОтправитьФайл()

Функция УдалитьФайл(ВходящиеДанные) Экспорт

	Результат = Ложь;

	ТипОбъекта = ТипЗнч(ВходящиеДанные);
	Если ТипОбъекта <> Тип("ОбъектDropbox") И ТипОбъекта <> Тип("Строка") Тогда
		ВызватьИсключение("Тип не поддерживается");
	КонецЕсли;

	ПутьНаСервисе = ?(ТипОбъекта = Тип("ОбъектDropbox"), ВходящиеДанные.ПутьПредставление, ВходящиеДанные);

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Content-Type", "application/json");

	Соединение = Новый HTTPСоединение("https://api.dropboxapi.com", 443);		
	Запрос = Новый HTTPЗапрос("2/files/delete_v2", Заголовки);

	ТелоЗапроса = Новый Соответствие();
	ТелоЗапроса.Вставить("path", ПутьНаСервисе);
	Запрос.УстановитьТелоИзСтроки(ПарсерJSON.ЗаписатьJSON(ТелоЗапроса));

	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния = 200 Тогда

		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ДанныеОтвета = ПарсерJSON.ПрочитатьJSON(ТелоОтвета);

		Metadata = ДанныеОтвета.Получить("metadata"); 
		Если Metadata = Неопределено Тогда
		
			ТекстОшибки = ТелоОтвета;
			Сообщить(ТекстОшибки);

		Иначе

			Результат = Истина;

		КонецЕсли;

	Иначе

		// обработка ошибок

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // УдалитьФайл()

Функция ПолучитьОписаниеФайла(ВходящиеДанные) Экспорт

	Результат = Неопределено;

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Content-Type", "application/json");

	Соединение = Новый HTTPСоединение("https://api.dropboxapi.com", 443);		
	Запрос = Новый HTTPЗапрос("2/files/get_metadata", Заголовки);

	ТелоЗапроса = Новый Соответствие();
	ТелоЗапроса.Вставить("path", ВходящиеДанные);
	Запрос.УстановитьТелоИзСтроки(ПарсерJSON.ЗаписатьJSON(ТелоЗапроса));
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);

	Если Ответ.КодСостояния = 200 Тогда

		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Результат = ПарсерJSON.ПрочитатьJSON(ТелоОтвета);

	Иначе

		// TODO: обработка ошибок
		Результат = Неопределено;

	КонецЕсли;

	Возврат Результат;

КонецФункции // ПолучитьОписаниеФайла()

Функция ОбъектDropbox(Знач ВходящиеДанные) Экспорт

	ДанныеЗаполнения = ПолучитьОписаниеФайла(ВходящиеДанные);
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	ОбъектDropbox = Новый ОбъектDropbox();
	ОбъектDropbox.ЗаполнитьИзJSON(ДанныеЗаполнения);

	Возврат ОбъектDropbox;

КонецФункции // ОбъектDropbox()

Функция ЗагрузитьФайл(Знач ВходящиеДанные, Знач ЛокальныйПуть) Экспорт

	Перем ПутьКФайлуНаСервисе;
	Перем Результат;

	ТипВходящихДанных = ТипЗнч(ВходящиеДанные);
	Если ТипВходящихДанных = Тип("Строка") Тогда
		ПутьКФайлуНаСервисе = ВходящиеДанные;
	ИначеЕсли ТипВходящихДанных = Тип("ОбъектDropbox") Тогда 
		ПутьКФайлуНаСервисе = ВходящиеДанные.ПутьПредставление; 
	Иначе
		ВызватьИсключение("Тип не поддерживается");
	КонецЕсли;

	// Если ВФорматеZIP И Не вРег(Прав(ЛокальныйПуть, 4)) = ".ZIP" Тогда
	// 	ВызватьИсключение("При загрузки файла в формате zip, локальный путь к файлу нужно указывать с расширением zip");
	// КонецЕсли;

	ПараметрыЗапроса = Новый Соответствие();
	ПараметрыЗапроса.Вставить("path", ПутьКФайлуНаСервисе);
	ПараметрыDropbox = ПолучитьФорматированнуюСтрокуJSON(ПараметрыЗапроса);

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Dropbox-API-Arg", ПараметрыDropbox);

	Соединение = Новый HTTPСоединение("https://content.dropboxapi.com", 443);
	// АдресЗапроса = 	СтрШаблон("2/files/%1", ?(ВФорматеZIP, "download_zip", "download"));
	АдресЗапроса = 	СтрШаблон("2/files/%1", "download");	
	Запрос = Новый HTTPЗапрос(АдресЗапроса, Заголовки);

	Ответ = Соединение.ОтправитьДляОбработки(Запрос);

	Если Ответ.КодСостояния = 200 Тогда

		ДД = Ответ.ПолучитьТелоКакДвоичныеДанные();
		ДД.Записать(ЛокальныйПуть);

		Результат = Истина;

	Иначе

		Сообщить(Ответ.ПолучитьТелоКакСтроку());
		// TODO: обработка ошибок
		Результат = Ложь;

	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ОткрытьСессиюОтправкиФайла(ЛокальныйПутьКФайлу) Экспорт
	
	Перем ИдентификаторСессии;
	
	Соединение = Новый HTTPСоединение("https://content.dropboxapi.com", 443);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Content-Type", "application/octet-stream");
	Заголовки.Вставить("Dropbox-API-Arg", ПолучитьФорматированнуюСтрокуJSON(Новый Структура()));
	
	Запрос = Новый HTTPЗапрос("2/files/upload_session/start", Заголовки);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		
		ДанныеОтвета = ПарсерJSON.ПрочитатьJSON(Ответ.ПолучитьТелоКакСтроку()); 	
		ИдентификаторСессии = ДанныеОтвета.Получить("session_id");
		
	Иначе
		
		// TODO: Обработка ошибок
		
	КонецЕсли;
	
	Возврат ИдентификаторСессии;
	
КонецФункции // ОткрытьСессиюОтправкиФайла()

Функция ОтправитьФайлЧерезСессию(ИдентификаторСессии, ЛокальныйПутьКФайлу, Размер, ПовторнаяПопытка = Ложь) Экспорт
	
	Результат = Ложь;
	
	ПараметрыСессии = Новый Структура("cursor", Новый Структура("session_id, offset", ИдентификаторСессии, Размер));
	ПараметрыDropbox = ПолучитьФорматированнуюСтрокуJSON(ПараметрыСессии);
	
	Соединение = Новый HTTPСоединение("https://content.dropboxapi.com", 443);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Content-Type", "application/octet-stream");
	Заголовки.Вставить("Dropbox-API-Arg", ПараметрыDropbox); 
	
	Запрос = Новый HTTPЗапрос(ВерсияAPI + "/files/upload_session/append_v2", Заголовки);
	Запрос.УстановитьИмяФайлаТела(ЛокальныйПутьКФайлу);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);	
	Если Ответ.КодСостояния = 200 Тогда
		
		Результат = Истина;
		
	Иначе
		
		// если не правильное смещение - пробуем еще раз
		УточненныйРазмер = Размер;
		Результат = ОтправитьФайлЧерезСессию(ИдентификаторСессии, ЛокальныйПутьКФайлу, УточненныйРазмер, Истина);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции // ОтправитьФайлЧерезСессию()

Функция ЗакрытьСесииюОтправкиФайла(Знач ВходящиеДанные, ИдентификаторСессии, ЛокальныйПутьКФайлу, 
	ПараметрыДействия, Размер, ПовторнаяПопытка = Ложь) Экспорт
	
	Если ТипЗнч(ВходящиеДанные) = Тип("Строка") Тогда
		ВходящиеДанные = Новый ОбъектDropbox();
	КонецЕсли;

	Соединение = Новый HTTPСоединение("https://content.dropboxapi.com", 443);
	Параметры = Новый Структура("cursor, commit", 
		Новый Структура("session_id, offset", ИдентификаторСессии, Размер), ПараметрыДействия);
	
	ПараметрыDropbox = ПолучитьФорматированнуюСтрокуJSON(Параметры);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Content-Type", "application/octet-stream");
	Заголовки.Вставить("Dropbox-API-Arg", ПараметрыDropbox);
	
	Запрос = Новый HTTPЗапрос(ВерсияAPI + "/files/upload_session/finish", Заголовки);
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		
		ДанныеОтвета = ПарсерJSON.ПрочитатьJSON(Ответ.ПолучитьТелоКакСтроку());
		ВходящиеДанные.ЗаполнитьИзJSON(ДанныеОтвета);
		// ИдентификаторФайла = ДанныеОтвета.Получить("id");
		
	Иначе
		
		Если Не ПовторнаяПопытка И Ответ.КодСостояния = 409 Тогда
			
			ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
			УточненныйРазмер = ПолучитьУточнениеРазмераИзОтвета(ТелоОтвета);
	
			Если УточненныйРазмер <> Неопределено Тогда

				Возврат ЗакрытьСесииюОтправкиФайла(ВходящиеДанные, ИдентификаторСессии, ЛокальныйПутьКФайлу, ПараметрыДействия, 
					УточненныйРазмер, Истина);

			КонецЕсли;

			ВходящиеДанные = Неопределено;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВходящиеДанные;
	
КонецФункции // ЗакрытьСесииюОтправкиФайла()

Функция ОтправитьФайлБезСессии(Знач ВходящиеДанные, ЛокальныйПутьКФайлу, ПутьНаСервисе, Режим = "add", Перемеименовывать = Ложь, 
	НеуведомлятьПользователей = Ложь, КонтролироватьСтруктуру = Ложь) Экспорт
	
	Если ТипЗнч(ВходящиеДанные) = Тип("Строка") Тогда
		ВходящиеДанные = Новый ОбъектDropbox();	
	КонецЕсли;
	
	ЛокальныйФайл = Новый Файл(ЛокальныйПутьКФайлу);
	Если Не ЛокальныйФайл.Существует() Тогда
		ВызватьИсключение ("Локальный файл не существует");
	КонецЕсли;
	
	ПараметрыОтправкиФайла = ПолучитьПараметрыОтправкиФайла(
	ПутьНаСервисе, 
	Режим, 
	Перемеименовывать, 
	НеуведомлятьПользователей, 
	КонтролироватьСтруктуру);	
	ПараметрыDropbox = ПолучитьФорматированнуюСтрокуJSON(ПараметрыОтправкиФайла);
	
	Соединение = Новый HTTPСоединение("https://content.dropboxapi.com");	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + ТокенАвторизации);
	Заголовки.Вставить("Content-Type", "application/octet-stream");
	Заголовки.Вставить("Dropbox-API-Arg", ПараметрыDropbox);
	
	Запрос = Новый HTTPЗапрос(ВерсияAPI + "/files/upload", Заголовки);
	Запрос.УстановитьИмяФайлаТела(ЛокальныйПутьКФайлу);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		
		ДанныеОтвета = ПарсерJSON.ПрочитатьJSON(Ответ.ПолучитьТелоКакСтроку());
		ВходящиеДанные.ЗаполнитьИзJSON(ДанныеОтвета);
		
	Иначе
		
		// TODO: обработка ошибок dropbox
		ВходящиеДанные = Неопределено;
		
	КонецЕсли;
	
	Возврат ВходящиеДанные;
	
КонецФункции // ОтправитьФайлБезСессии()

Функция ПолучитьФорматированнуюСтрокуJSON(Данные) Экспорт
	
	Возврат ПарсерJSON.ЗаписатьJSON(Данные, Ложь,, Истина, Истина);
	
КонецФункции // ПолучитьФорматированнуюСтрокуJSON()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьХэшАвторизации()
	
	Возврат КодироватьСтрокуВBase64(КлючАвторизации + ":" + СекретныйКлюч);
	
КонецФункции // ПолучитьХэшАвторизации()

Функция КодироватьСтрокуВBase64(ИсходнаяСтрока, Знач КодировкаФайла = Неопределено) 
	
	ИмяФайла = ПолучитьИмяВременногоФайла();
	
	Если КодировкаФайла = Неопределено Тогда
		КодировкаФайла = КодировкаТекста.ANSI;
	КонецЕсли;
	
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаФайла);
	ЗаписьТекста.Записать(ИсходнаяСтрока);
	ЗаписьТекста.Закрыть();
	Результат = Base64Строка(Новый ДвоичныеДанные(ИмяФайла));
	
	Попытка 
		УдалитьФайлы(ИмяФайла) 
	Исключение 
		Сообщить("Не удалось удалить временный файл, по причине: " + ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции // КодироватьСтрокуВBase64()

Функция ПолучитьОграничениеРазмераФайла()
	
	Возврат 150 * 1024 * 1024 * 8; // 150 мБайт
	
КонецФункции // ПолучитьОграничениеРазмераФайла()

Функция ПолучитьПараметрыОтправкиФайла(ПутьНаСервисе, Режим = "add", Перемеименовывать = Истина, 
	НеуведомлятьПользователей = Ложь, КонтролироватьСтруктуру = Ложь)
	
	Параметры = Новый Структура();
	Параметры.Вставить("path", ПутьНаСервисе);
	Параметры.Вставить("mode", Режим);
	Параметры.Вставить("autorename", Перемеименовывать);
	Параметры.Вставить("mute", НеуведомлятьПользователей);
	Параметры.Вставить("strict_conflict", КонтролироватьСтруктуру);
	
	Возврат Параметры;
	
КонецФункции // ПолучитьПараметрыОтправкиФайла()

Функция ПолучитьУточнениеРазмераИзОтвета(Тело)
	
	Перем Результат;
	
	ДанныеОтвета = ПарсерJSON.ПрочитатьJSON(Тело);
	Если СтрНайти(ДанныеОтвета.Получить("error_summary"), "incorrect_offset") > 0 Тогда		
		Ошибка = ДанныеОтвета.Получить("error");
		Если Ошибка <> Неопределено Тогда		
			Результат = Ошибка.Получить("correct_offset");	
			Если Результат = Неопределено Тогда
				ТегПоиска = Ошибка.Получить(".tag");		
				Если ТегПоиска <> Неопределено Тогда
					Уровень2 = Ошибка.Получить(ТегПоиска);
					Если Уровень2 <> Неопределено Тогда
						Результат = Уровень2.Получить("correct_offset");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;

	Возврат Результат;

КонецФункции // ПолучитьУточнениеРазмераИзОтвета()

#КонецОбласти

КлючАвторизации = "";
СекретныйКлюч = "";
ВерсияAPI = "2";
ПарсерJSON = Новый ПарсерJSON();
